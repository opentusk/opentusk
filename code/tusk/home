<%doc>
 Copyright 2012 Tufts University 

 Licensed under the Educational Community License, Version 1.0 (the "License"); 
 you may not use this file except in compliance with the License. 
 You may obtain a copy of the License at 

 http://www.opensource.org/licenses/ecl1.php 

 Unless required by applicable law or agreed to in writing, software 
 distributed under the License is distributed on an "AS IS" BASIS, 
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
 See the License for the specific language governing permissions and 
 limitations under the License.
</%doc>

<%attr>
	page_header      => '/tmpl/prepend:manage_header_div_layout'
	page_footer      => '/tmpl/footer:footer_div_with_school_links'
	no_check_path    => 1
	top_tab_type     => 'home'
	allow_shib       => 1
	nostandardheader => 1
	body_id          => 'home'
</%attr>

<%once>
	use TUSK::Core::School;
	use TUSK::Constants;
</%once>

<%shared>
	my $user = $m->session->{'user'};
	my $user_id = $user->primary_key();
	my %course_hash;
</%shared>

<%perl>
	my @author_courses = $user->author_courses();
	foreach my $course (@author_courses){
		if ($course->type() eq 'group'){
			push (@{$course_hash{$course->school()}{"my_groups"}}, $course);
		} elsif ($course->type() eq 'committee' || $course->type() eq 'thesis committee') {
			push (@{$course_hash{$course->school()}{"user_committees"}}, $course);
		} elsif ($course->type() eq 'community service' or $course->type() eq 'course' or $course->type() eq 'integrated course') {
			push (@{$course_hash{$course->school()}{"admin_courses"}}, $course);
		}  
	}

	my $user_group_courses = $user->get_user_group_courses_with_categories();
	foreach my $school (keys %$user_group_courses) {
		$course_hash{$school}{"student_courses"}{"user_group"} = $user_group_courses->{$school};
	}

	foreach my $enrollment_course ($user->get_enrollment_courses()) {
		push (@{$course_hash{$enrollment_course->school()}{"student_courses"}{"enrollment"}}, $enrollment_course);
	}


	my $upcoming = {};
	my @schools = ();
	my $counter = 0;
	my $affiliation = $user->affiliation_or_default_school();
	my $max_school_tabs = 3;

	if (scalar keys %course_hash) {
 		foreach my $school (keys %course_hash) {
			my $temp_arrayref = $user->get_important_upcoming_dates_by_school($school);
			$upcoming->{$school} = [];
			foreach my $event (@$temp_arrayref) {
				if ($event->{type} eq "schedule") {
					$event->{link} = "/view/course/$school/" . $event->{course_id} . "/schedule/" . $event->{id};
				} elsif ($event->{type} eq "quiz") {
					$event->{link} = "/quiz/quizstart/course/$school/" . $event->{course_id} . "/" . $event->{id};
				} elsif ($event->{type} eq "case") {
					$event->{link} = "/case/casestart/" . TUSK::Core::School->new->getSchoolID($school) . "/" . $event->{course_id} . "/" . $event->{id};
				} elsif ($event->{type} eq "eval") {
					$event->{link} = "/eval/complete/$school/" . $event->{id};
				} elsif ($event->{type} eq "assignment") {
					$event->{link} = "/assignment/student/complete/course/$school/" . $event->{course_id} . "/" . $event->{id};
				} 
				push @{$upcoming->{$school}}, $event;
			}
			$counter++;
	 	}

		## if user has an affiliation in the school list, make that the first so it's the active tab on the home page
		if ( grep { $_ eq $affiliation } keys %course_hash ) {
			push @schools, $affiliation;
			push @schools, grep { $_ ne $affiliation } keys %course_hash;
		} else {
			@schools = ((keys %course_hash), $affiliation);
		}
	} else {
		push @schools, $affiliation;
		$upcoming->{$affiliation} = $user->get_important_upcoming_dates_by_school($affiliation);
	}

	$m->comp("/tmpl/home:school_announcements", user => $user);

	$m->comp("/tmpl/home:main_user_tabs", 
		user => $user, 
		course_hash => \%course_hash, 
		upcoming => $upcoming, 
		today => $user->todays_sorted_meetings_by_school(), 
		schools => (scalar @schools >= $max_school_tabs) ? [ @schools[0..$max_school_tabs-1] ] : \@schools, 
		schedule_ugs => $user->has_schedule());
</%perl>

<%method title>
% return $TUSK::Constants::SiteAbbr ." ".__("Home");
</%method>

<%method jsarray>
% return ['jquery/jquery.min.js','jquery/jquery.ui.core.min.js','jquery/jquery.ui.widget.min.js','jquery/jquery.ui.position.min.js','jquery/jquery.ui.dialog.min.js','jquery/jquery.ui.button.min.js','home.js'];
</%method>

<%method stylearray>
% return ['/style/style.css','/style/home.css'];
</%method>

<%method leftnav>
<%perl>
my %someHash = (
                'component'   => "/tmpl/leftnav/home",
                'show_in_td'  => 0,
               );
return \%someHash;
</%perl>
</%method>

