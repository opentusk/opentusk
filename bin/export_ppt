#!/usr/bin/perl
# Copyright 2012 Tufts University
#
# Licensed under the Educational Community License, Version 1.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.opensource.org/licenses/ecl1.php
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

use FindBin;
use lib "$FindBin::Bin/../lib";

use strict;
use warnings;

use HSDB4::Constants;
use HSDB45::Course;

my $dest_dir = shift || '/export/ppt';

our %exported = ();

my $dbh = HSDB4::Constants::def_db_handle();
my $sql = 'SELECT course_id, title FROM hsdb45_vet_admin.course';
my $sth = $dbh->prepare($sql);
$sth->execute();
my $results = $sth->fetchall_arrayref();
$sth->finish();

foreach my $result (@$results) {
    my $course_id = $result->[0];
    my $title = safe($result->[1]);

    my $path = "$dest_dir/$title-$course_id";
    my $course = HSDB45::Course->new(_school => 3)->lookup_key($course_id);
    export($course_id, $path, $course->child_content());
}

sub export {
    my $course_id = shift;
    my $path = shift;

    foreach my $content (@_) {
        my $content_id = $content->content_id();
        next if $exported{"$course_id/$content_id"};
        $exported{"$course_id/$content_id"} = 1;
        my $type = $content->type();
        my $title = safe($content->title());
        if ($type =~ /Collection|Multidocument/) {
            export($course_id, "$path/$title-$content_id", $content->child_content())
        } elsif ($type eq 'DownloadableFile') {
            my $file = $content->out_file_path();
            if ($file =~ m/^.*\.pp[st]x?$/i) {
                my $name = $file;
                $name =~ s/\/.*\///;
                $name = "$title-$name" if ($title);
                print "$file -> $path/$name\n";
                system("mkdir -p $path && rsync -t $file $path/$name");
            }
        }
    }
}

sub safe {
    my $title = shift;

    if ($title) {
        $title =~ s/\W+/_/g;
        $title =~ s/^_|_$//g;
    }

    return $title;
}
