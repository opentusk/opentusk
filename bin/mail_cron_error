#!/usr/bin/env perl
#
# Copyright 2012 Tufts University 
#
# Licensed under the Educational Community License, Version 1.0 (the "License"); 
# you may not use this file except in compliance with the License. 
# You may obtain a copy of the License at 
#
# http://www.opensource.org/licenses/ecl1.php 
#
# Unless required by applicable law or agreed to in writing, software 
# distributed under the License is distributed on an "AS IS" BASIS, 
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
# See the License for the specific language governing permissions and 
# limitations under the License.



use FindBin;
use lib "$FindBin::Bin/../lib";
use strict;
use TUSK::Constants;

use Getopt::Long;
use Net::SMTP;

my ($to, $from, @ccs, @data);
my $subject='';
my $debug=0;

# should be pulled from a central configuration.
my $server = 'localhost';

GetOptions(
	'help!' => \&usage,
	'to=s' => \$to,
	'server=s' => \$server,
	'from=s' => \$from,
	'debug' => \$debug,
);

sub usage {
	print "Usage: $0 [--help] [--to ADDRESS] [--from ADDRESS] [--server SERVER] [ADDRESS(s)] SUBJECT\n";
	print "\t--help\t\tThis help screen\n";
	print "\t--to ADDRESS\toverride the addresses to send mail to\n";
	print "\t--server SERVER\tUse SERVER as the outbound SMTP server (defaults to localhost)\n";
	print "\t--from ADDRESS\toverride the from address in the mail\n";
	print "\tADDRESS\t\tCc the mail to the following addresses\n";
	print "\tSUBJECT\t\tThe required Subject of the mail\n";
	exit 0;
}

foreach (@ARGV) {
	if(/\@/) {push @ccs, $_;}
	else     {$subject.="$_ ";}
}
chomp $subject;

unless($subject) {die "Error: you must pass a subject line for $0\n";}

$to =  $TUSK::Constants::ErrorEmail
	if ( ! defined $to );

$from =  $TUSK::Constants::ErrorEmail
	if ( ! defined $from );

my (@errors) = <STDIN>;

# Grep out uninteresting lines
@errors = grep { ! /^Scalars leaked/ } @errors;
@errors = grep {
    ! /^Error: Illegal entry in bfchar block in ToUnicode CMap/
  } @errors;

if(scalar(@errors) == 0) {exit(0);}

my $smtp = Net::SMTP->new($server, Debug => $debug) || die "Could not open SMTP connection to $server\n";

$smtp->mail($from);
$smtp->to($to);
$smtp->cc(@ccs) if ( scalar @ccs );

# setup the header information.
push @data, ( "From: $from\n", "To: $to\n", "Subject: $subject\n", );
push @data, "Cc: " . join(",", @ccs) if ( scalar @ccs );
push @data, "\n" ;

# Now push the message.
push @data, @errors;

$smtp->data(@data);
$smtp->quit;

exit(0);
